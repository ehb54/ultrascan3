# admin/cmake/GenerateVersion.cmake
# Generates version and build metadata

# Get version from CMake project (numeric only)
set(VERSION_MAJOR ${PROJECT_VERSION_MAJOR})
set(VERSION_MINOR ${PROJECT_VERSION_MINOR})
set(VERSION_PATCH ${PROJECT_VERSION_PATCH})

# Get full version string with suffix from US3_VERSION (extracted from us_defines.h)
# US3_VERSION contains the full version including any suffix like -dev
if(US3_VERSION)
    set(VERSION_STRING "${US3_VERSION}")
else()
    # Fallback if US3_VERSION is not set
    set(VERSION_STRING "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}")
endif()

if(NOT DEFINED US3_SOURCE_DIR)
    set(US3_SOURCE_DIR "${CMAKE_CURRENT_LIST_DIR}/../..")
endif()
if(NOT DEFINED US3_BINARY_DIR)
    set(US3_BINARY_DIR "${CMAKE_CURRENT_LIST_DIR}/../..")
endif()

# Get git commit count as build number
execute_process(
        COMMAND git rev-list --count HEAD
        WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
        OUTPUT_VARIABLE BUILD_NUMBER
        OUTPUT_STRIP_TRAILING_WHITESPACE
        ERROR_QUIET
)

if(NOT BUILD_NUMBER)
    set(BUILD_NUMBER "0")
endif()

# Get git commit hash
execute_process(
        COMMAND git rev-parse --short HEAD
        WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
        OUTPUT_VARIABLE GIT_COMMIT_HASH
        OUTPUT_STRIP_TRAILING_WHITESPACE
        ERROR_QUIET
)

if(NOT GIT_COMMIT_HASH)
    set(GIT_COMMIT_HASH "unknown")
endif()

# Get git branch
execute_process(
        COMMAND git rev-parse --abbrev-ref HEAD
        WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
        OUTPUT_VARIABLE GIT_BRANCH
        OUTPUT_STRIP_TRAILING_WHITESPACE
        ERROR_QUIET
)

if(NOT GIT_BRANCH)
    set(GIT_BRANCH "unknown")
endif()

# Check if working directory is dirty
execute_process(
        COMMAND git diff-index --quiet HEAD --
        WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
        RESULT_VARIABLE GIT_DIRTY
        ERROR_QUIET
)

if(GIT_DIRTY)
    set(GIT_DIRTY_FLAG "-dirty")
else()
    set(GIT_DIRTY_FLAG "")
endif()

# Get current timestamps
string(TIMESTAMP BUILD_STAMP "%Y-%m-%d %H:%M:%S" UTC)

# "2025-11-04 16:23:01" -> BUILD_DATE="2025-11-04", BUILD_TIME="16:23:01"
string(SUBSTRING "${BUILD_STAMP}" 0 10 BUILD_DATE)
string(SUBSTRING "${BUILD_STAMP}" 11 8 BUILD_TIME)

# Remove '-', ' ', ':' to get a compact timestamp: "20251104162301"
string(REGEX REPLACE "[-: ]" "" BUILD_TIMESTAMP "${BUILD_STAMP}")


# Construct full version string with build number
set(VERSION_FULL "${VERSION_STRING}+${BUILD_NUMBER}")

# Determine OS title
if(WIN32)
    set(OS_TITLE "Windows")
elseif(APPLE)
    set(OS_TITLE "macOS")
elseif(UNIX)
    set(OS_TITLE "Linux")
else()
    set(OS_TITLE "Unknown")
endif()

# create us_revision.h for legacy code that includes it directly
file(WRITE ${US3_SOURCE_DIR}/programs/us/us_revision.h
        "#ifndef US_REVISION_H
#define US_REVISION_H

/* Auto-generated by CMake: DO NOT EDIT. */
#define BUILDNUM     \"${BUILD_NUMBER}\"
#define BUILD_DATE   \"${BUILD_DATE}\"
#define BUILD_TIME   \"${BUILD_TIME}\"

#endif /* US_REVISION_H */
")

message(STATUS "Writing us_revision.h to: ${US3_SOURCE_DIR}/programs/us/us_revision.h")
message(STATUS "Generated version ${VERSION_FULL} (${GIT_COMMIT_HASH}${GIT_DIRTY_FLAG} on ${GIT_BRANCH})")