# admin/cmake/GenerateVersion.cmake
# Generates version and build metadata
# Only updates us_revision.h if content actually changes

# Get version from CMake project (numeric only)
set(VERSION_MAJOR ${PROJECT_VERSION_MAJOR})
set(VERSION_MINOR ${PROJECT_VERSION_MINOR})
set(VERSION_PATCH ${PROJECT_VERSION_PATCH})

# Get full version string with suffix from US3_VERSION (extracted from us_defines.h)
# US3_VERSION contains the full version including any suffix like -dev
if(US3_VERSION)
    set(VERSION_STRING "${US3_VERSION}")
else()
    # Fallback if US3_VERSION is not set
    set(VERSION_STRING "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}")
endif()

if(NOT DEFINED US3_SOURCE_DIR)
    set(US3_SOURCE_DIR "${CMAKE_CURRENT_LIST_DIR}/../..")
endif()
if(NOT DEFINED US3_BINARY_DIR)
    set(US3_BINARY_DIR "${CMAKE_CURRENT_LIST_DIR}/../..")
endif()

# Get git commit count as build number
execute_process(
        COMMAND git rev-list --count HEAD
        WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
        OUTPUT_VARIABLE BUILD_NUMBER
        OUTPUT_STRIP_TRAILING_WHITESPACE
        ERROR_QUIET
)

if(NOT BUILD_NUMBER)
    set(BUILD_NUMBER "0")
endif()

execute_process(
        COMMAND git rev-parse --short=7 HEAD
        WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
        OUTPUT_VARIABLE GIT_REVISION
        OUTPUT_STRIP_TRAILING_WHITESPACE
        ERROR_QUIET
)

if(NOT GIT_REVISION)
    set(GIT_REVISION "unknown")
endif()

# Get git branch
execute_process(
        COMMAND git rev-parse --abbrev-ref HEAD
        WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
        OUTPUT_VARIABLE GIT_BRANCH
        OUTPUT_STRIP_TRAILING_WHITESPACE
        ERROR_QUIET
)

if(NOT GIT_BRANCH)
    set(GIT_BRANCH "unknown")
endif()

# Check for local changes (modified, added, deleted, or untracked files)
# Dirty = tracked changes only (ignore untracked files)
set(GIT_DIRTY_FLAG "")
set(LOCAL_CHANGES "")

# 1) Unstaged changes to tracked files?
execute_process(
        COMMAND git diff --quiet --exit-code
        WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
        RESULT_VARIABLE GIT_DIFF_WORKTREE_RC
        ERROR_QUIET
)

# 2) Staged changes to tracked files?
execute_process(
        COMMAND git diff --quiet --cached --exit-code
        WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
        RESULT_VARIABLE GIT_DIFF_INDEX_RC
        ERROR_QUIET
)

# If either diff is non-zero, tracked content differs
if(NOT GIT_DIFF_WORKTREE_RC EQUAL 0 OR NOT GIT_DIFF_INDEX_RC EQUAL 0)
    set(GIT_DIRTY_FLAG "-dirty")
    set(LOCAL_CHANGES " Î”")
endif()

# Get commit date (last commit date) instead of current build timestamp
execute_process(
        COMMAND env TZ=UTC0 git log -1
        --date=format:%Y-%m-%d\ %H:%M:%S\ UTC
        --format=%cd
        WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
        OUTPUT_VARIABLE REVISION_DATE
        OUTPUT_STRIP_TRAILING_WHITESPACE
        ERROR_QUIET
)

if(NOT REVISION_DATE)
    set(REVISION_DATE "unknown")
endif()

# Construct full version string with build number
set(VERSION_FULL "${VERSION_STRING}+${BUILD_NUMBER}")

# Generate the NEW content
set(NEW_CONTENT "#ifndef US_REVISION_H
#define US_REVISION_H

/* Auto-generated by CMake: DO NOT EDIT. */
#define BUILDNUM        \"${BUILD_NUMBER}\"
#define GIT_REVISION    \"${GIT_REVISION}\"
#define REVISION_DATE   \"${REVISION_DATE}\"
#define LOCAL_CHANGES   \"${LOCAL_CHANGES}\"

#endif /* US_REVISION_H */
")

set(REVISION_HEADER "${US3_SOURCE_DIR}/programs/us/us_revision.h")

# Read existing content if file exists
set(OLD_CONTENT "")
if(EXISTS "${REVISION_HEADER}")
    file(READ "${REVISION_HEADER}" OLD_CONTENT)
endif()

# Only write if content has changed
if(NOT "${OLD_CONTENT}" STREQUAL "${NEW_CONTENT}")
    file(WRITE "${REVISION_HEADER}" "${NEW_CONTENT}")
    message(STATUS "Updated us_revision.h: build ${BUILD_NUMBER}, rev ${GIT_REVISION}${LOCAL_CHANGES}, ${REVISION_DATE}")
else()
    message(STATUS "us_revision.h unchanged: build ${BUILD_NUMBER}, rev ${GIT_COMMIT_HASH}${LOCAL_CHANGES}")
endif()

message(STATUS "Generated version ${VERSION_FULL} (${GIT_COMMIT_HASH}${GIT_DIRTY_FLAG} on ${GIT_BRANCH})")
